#pragma kernel RenderBackground; // 0
#pragma kernel RenderFluids; // 1
#pragma kernel RenderRigidBodies; // 2
#pragma kernel RenderUI; // 3

#pragma multi_compile _ DRAW_RB_CENTROIDS
#pragma multi_compile _ DRAW_FLUID_OUTLINES
#pragma multi_compile _ DRAW_RB_OUTLINES
#pragma multi_compile _ USE_METABALLS

#include "MathResources.hlsl"
#include "Composition.hlsl"
#include "DataTypes.hlsl"
#include "Constants.hlsl"

const float VisualParticleRadii;
const float MetaballsThreshold;
const float MetaballsEdgeDensityWidth;
const float FluidEdgeWidth;
const float RBEdgeWidth;
const int RBVectorNum;
const int2 Resolution;

const int2 BoundsDims;
const float InvMaxInfluenceRadius;
const int MaxInfluenceRadius;
const int MaxInfluenceRadiusSqr;
const int NumMaterials;
const int2 ChunksNum;
const int ParticlesNum;
const int RBodiesNum;

RWStructuredBuffer<int> RecordedElements;
RWTexture2D<unorm float4> Result;

Texture2D<unorm float4> Caustics;
Texture2D<unorm float4> UITexture;
Texture2D<unorm float4> Background;
Texture2D<unorm float4> Atlas;

StructuredBuffer<int2> SpatialLookup; 
StructuredBuffer<int> StartIndices;

StructuredBuffer<PData> PDatas;
StructuredBuffer<PType> PTypes;

StructuredBuffer<RigidBody> RigidBodies;
StructuredBuffer<RBVector> RBVectors;

StructuredBuffer<Mat> Materials;

float3 SampleTexture(float2 uv, uint2 texLoc, uint2 texDims, Texture2D<unorm float4> tex)
{
    uint2 texelCoord = texLoc + (uint2)(uv * float2(texDims.x, texDims.y));
    float3 sampleCol = tex.Load(int3(texelCoord, 0)).rgb;
 
    return sampleCol;
}

bool ValidChunk(int2 chunk)
{
    return chunk.x >= 0 && chunk.x < ChunksNum.x && chunk.y >= 0 && chunk.y < ChunksNum.y;
}

int GetChunkKey(int2 chunk)
{
    return chunk.y * ChunksNum.x + chunk.x;
}

uint GetPixelKey(uint2 threadID)
{
    return threadID.y * Resolution.x + threadID.x;
}

float3 TemperatureToRGB(float temperature)
{
    float normalizedTemp = saturate((temperature - 273.0) / (373.0 - 273.0));

    float3 color;

    if (normalizedTemp < 0.1)
    {
        // Light blue to dark blue transition
        color = lerp(float3(0.5, 0.5, 1.0), float3(0.0, 0.0, 1.0), 2.5 * normalizedTemp);
    }
    else if (normalizedTemp < 0.5)
    {
        // Dark blue to green transition
        color = lerp(float3(0.0, 0.0, 1.0), float3(0.0, 1.0, 0.0), 2.5 * (normalizedTemp - 0.1));
    }
    else
    {
        // Green to red transition
        color = lerp(float3(0.0, 1.0, 0.0), float3(1.0, 0.0, 0.0), 2.5 * (normalizedTemp - 0.5));
    }

    return color;
}

float3 VelocityToRGB(float2 velocity, float g)
{
    // Not really normalized
    float normalizedVelLength = length(velocity) * 0.04;

    float r = min(1.0, normalizedVelLength);
    float b = max(0.0, 1 - normalizedVelLength);

    return float3(r, g, b);
}

bool IsPointInsideRB(float2 pos, RigidBody rb)
{
    // Transform the point position into the local coordinate space of the rigid body
    float2 localPos = pos - rb.pos;

    uint intersections = 0;
    uint startIndex = rb.startIndex;
    uint endIndex = rb.endIndex;
    uint numVertices = endIndex - startIndex + 1;

    float2 lastVec = RBVectors[startIndex].pos;
    for (uint i = 0; i < numVertices; i++)
    {
        uint vecIndex = wrapUint(startIndex + i, startIndex, endIndex);
        float2 newVec = RBVectors[vecIndex].pos;

        // Check if the edge from lastVec to newVec intersects the horizontal ray to the right of localPos
        if (IsPointToTheLeftOfLine(localPos, lastVec, newVec)) intersections++;

        lastVec = newVec;
    }

    // The point is inside the polygon if the number of intersections is odd
    bool isInside = (intersections % 2) == 1;

    return isInside;
}

float DstToRB(float2 pos, RigidBody rb)
{
    // Keep the last vector stored to avoid redundant data fetches
    RBVector lastVec = RBVectors[rb.startIndex];

    float minDstSqr = 1.#INF;
    uint startIndex = rb.startIndex;
    uint endIndex = rb.endIndex;
    for (uint vecIndex = startIndex + 1; vecIndex <= endIndex; vecIndex++)
    {
        RBVector newVec = RBVectors[wrapUint(vecIndex, startIndex, endIndex)];

        float2 dst = DstToLineSegment(lastVec.pos + rb.pos, newVec.pos + rb.pos, pos);

        float dstSqr = dot2(dst);
        if (dstSqr < minDstSqr) minDstSqr = dstSqr;

        lastVec = newVec;
    }

    float dst = minDstSqr == 1.#INF ? 1.#INF : sqrt(minDstSqr);

    return dst;
}

float MetaballsDensity(float dstSqr, float invRadius)
{
    float dstR = sqrt(dstSqr) * invRadius;
    return (1 - dstR);
}

float4 GetRenderColor(PData pData)
{
    // visualize velocity / temperature
    float3 rgb = VelocityToRGB(pData.vel, 0);
    // float3 rgb = TemperatureToRGB(pData.temperature);

    return float4(rgb.x, rgb.y, rgb.z, 0);
}

float3 SampleCaustics(uint2 threadID, float3 sampleColMul)
{
    float t;
    float2 causticsTexDims;
    float2 baseRes = 512;
    Caustics.GetDimensions(0, causticsTexDims.x, causticsTexDims.y, t);

    uint2 wrappedThreadID = (threadID / (baseRes / causticsTexDims)) % causticsTexDims;

    float3 fluidCol = sampleColMul * Caustics[wrappedThreadID].rgb;

    return fluidCol;
}

[numthreads(TN_R,TN_R,1)]
void RenderBackground(uint3 id : SV_DispatchThreadID)
{
    Result[id.xy] = Background[id.xy];
}
 
[numthreads(TN_R,TN_R,1)]
void RenderFluids(uint3 id : SV_DispatchThreadID)
{
    float2 pixelPos = (float2(id.xy) + .5) * BoundsDims / (float2)Resolution;
    int2 chunk = (int2)(pixelPos * InvMaxInfluenceRadius);

    int2 curChunk;
    int lastChunkKey_PType_POrder;
    float totDensity = 0;
    bool particleFound = false;
    bool drawOutline = false;
    bool doContinue = true;
    #if !USE_METABALLS
        float minRadius = min(MaxInfluenceRadius, VisualParticleRadii);
        float minRadiusSqr = minRadius*minRadius;
        float minDstSqr = 1.#INF;
    #endif
    for (int x = -1; x <= 1 && doContinue; x++)
    {
        for (int y = -1; y <= 1 && doContinue; y++)
        {
            curChunk = chunk + int2(x, y);

            if (!ValidChunk(curChunk)) continue;

            int chunkKey = GetChunkKey(curChunk);
            int startIndex = StartIndices[chunkKey];

            int index = startIndex; 
            while (index < ParticlesNum && chunkKey == SpatialLookup[index].y)
            {
                int pIndex = SpatialLookup[index].x;
                PData pData = PDatas[pIndex];

                float dstSqr = dot2(pixelPos - pData.pos);

                #if USE_METABALLS
                    if (dstSqr < MaxInfluenceRadiusSqr)
                    {
                        totDensity += MetaballsDensity(dstSqr, InvMaxInfluenceRadius);

                        #if DRAW_FLUID_OUTLINES
                            if (totDensity > MetaballsThreshold - MetaballsEdgeDensityWidth)
                            {
                                drawOutline = true;
                                lastChunkKey_PType_POrder = pData.lastChunkKey_PType_POrder;
                                particleFound = true;
                            }
                        #endif

                        if (totDensity > MetaballsThreshold)
                        {
                            #if DRAW_FLUID_OUTLINES
                                drawOutline = false;
                            #else
                                lastChunkKey_PType_POrder = pData.lastChunkKey_PType_POrder;
                                particleFound = true;
                            #endif
                            doContinue = false;
                            break;
                        }
                    }
                #else
                    if (dstSqr < min(minRadiusSqr, minDstSqr))
                    {
                        lastChunkKey_PType_POrder = pData.lastChunkKey_PType_POrder;
                        particleFound = true;
                        minDstSqr = dstSqr;

                        #if DRAW_FLUID_OUTLINES
                            if (sqrt(dstSqr) > minRadius - FluidEdgeWidth) drawOutline = true;
                            else drawOutline = false;
                        #endif
                    }
                #endif

                // Increment index each iteration - Chunk particle search algorithm
                index++;
            }
        }
    }

    if (particleFound)
    {
        Mat mat = Materials[PTypes[Extract_PType(lastChunkKey_PType_POrder)].matIndex];

        float3 result;
        if (drawOutline)
        {
            result = mat.edgeCol;
        }
        else
        {
            float3 color = SampleCaustics(id.xy, mat.sampleColMul) + mat.baseCol;
            
            float opacity = mat.opacity;
            float transparency = 1 - opacity;
        
            result = transparency > 0 ? color * opacity + transparency * Background[id.xy].rgb : color;
        }
    
        Result[id.xy] = float4(result, 1);
    }
}

float2 MirrorRepeatUV(float2 uv)
{
    float2 intPart;
    float2 fracPart = frac(uv); // Get fractional part

    // Determine if each component should be mirrored
    float2 isOdd = floor(uv) - floor(uv / 2.0) * 2.0; // 0.0 for even, 1.0 for odd

    // Mirror the fractional part where the integer part is odd
    fracPart = lerp(fracPart, 1.0 - fracPart, step(0.5, isOdd));

    return fracPart;
}

[numthreads(TN_R,TN_R,1)]
void RenderRigidBodies(uint3 id : SV_DispatchThreadID)
{
    float2 pixelPos = (float2(id.xy) + .5) * BoundsDims / (float2)Resolution;

    int highestRenderPriority = 0;
    bool rigidBodyFound = false;
    bool drawOutline = false;
    int matIndex;
    float3 transformData = 0;
    for (int rbIndex = 0; rbIndex < RBodiesNum; rbIndex++)
    {
        RigidBody rb = RigidBodies[rbIndex];

        float dstSqr = dot2(pixelPos - rb.pos);

        #if DRAW_RB_CENTROIDS
            if (dstSqr < CENTROID_RADIUS_SQR)
            {
                Result[id.xy] = COL_RED;
                return;
            }
        #endif

        int renderPriority = rb.renderPriority;
        if (renderPriority > highestRenderPriority && dstSqr < rb.maxRadiusSqr)
        {
            if (IsPointInsideRB(pixelPos, rb))
            {
                highestRenderPriority = renderPriority;
                rigidBodyFound = true;
                matIndex = rb.matIndex;
                transformData = float3(rb.pos.x, rb.pos.y, rb.totRot);

                #if DRAW_RB_OUTLINES
                    float dst = DstToRB(pixelPos, rb);
                    if (dst < RBEdgeWidth) drawOutline = true;
                    else drawOutline = false;
                #endif
            }
        }
    }

    if (rigidBodyFound)
    {
        Mat mat = Materials[matIndex];

        float3 result;
        if (drawOutline)
        {
            result = mat.edgeCol;
        }
        else
        {
            float2 localPixelPos = rotate2d(pixelPos - transformData.xy, -transformData.z);
            float2 localUV = MirrorRepeatUV(mat.colTexScale * localPixelPos / (float2)BoundsDims);
    
            float3 color = mat.colTexLoc.x != -1 ? SampleTexture(localUV, mat.colTexLoc, mat.colTexDims, Atlas) * mat.sampleColMul + mat.baseCol : mat.baseCol;
            
            float opacity = mat.opacity;
            float transparency = 1 - opacity;
    
            result = transparency > 0 ? color * opacity + transparency * Background[id.xy].rgb : color;
        }

        Result[id.xy] = float4(result, 1);
    }
}
 
[numthreads(TN_R,TN_R,1)]
void RenderUI(uint3 id : SV_DispatchThreadID)
{
    float4 uiColor = UITexture[id.xy];
    if (uiColor.a != 0)
    {
        Result[id.xy] = uiColor;
        return;
    }
}