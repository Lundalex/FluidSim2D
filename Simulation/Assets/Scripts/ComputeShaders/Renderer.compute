#pragma kernel Render2D; // 0

#include "MathResources.hlsl"
#include "CompositionResources.hlsl"
#include "DataTypes.hlsl"
#include "Constants.hlsl"

const float VisualParticleRadii;
const float RBRenderThickness;
const int RBVectorNum;
const int ResolutionX;
const int ResolutionY;

const int Width;
const int Height;
const int MaxInfluenceRadius;
const int2 ChunksNum;
const int ParticlesNum;
const int RBodiesNum;

RWTexture2D<unorm float4> Result;

StructuredBuffer<int2> SpatialLookup; 
StructuredBuffer<int> StartIndices;

StructuredBuffer<PType> PTypes;
StructuredBuffer<PData> PDatas;

StructuredBuffer<RBData> RBDatas;
StructuredBuffer<RBVector> RBVectors;

bool ValidChunk(int ChunkX, int ChunkY)
{
    return ChunkX >= 0 && ChunkX < ChunksNum.x && ChunkY >= 0 && ChunkY < ChunksNum.y;
}

float3 TemperatureToRGB(float temperature)
{
    float normalizedTemp = saturate((temperature - 273.0) / (373.0 - 273.0));

    float3 color;

    if (normalizedTemp < 0.1)
    {
        // Light blue to dark blue transition
        color = lerp(float3(0.5, 0.5, 1.0), float3(0.0, 0.0, 1.0), 2.5 * normalizedTemp);
    }
    else if (normalizedTemp < 0.5)
    {
        // Dark blue to green transition
        color = lerp(float3(0.0, 0.0, 1.0), float3(0.0, 1.0, 0.0), 2.5 * (normalizedTemp - 0.1));
    }
    else
    {
        // Green to red transition
        color = lerp(float3(0.0, 1.0, 0.0), float3(1.0, 0.0, 0.0), 2.5 * (normalizedTemp - 0.5));
    }

    return color;
}

float3 VelocityToRGB(float2 velocity, float g)
{
    // Not really normalized
    float normalizedVelLength = length(velocity) * 0.1;

    float r = min(1.0, normalizedVelLength);
    float b = max(0.0, 1 - normalizedVelLength);

    return float3(r, g, b);
}

uint WrapUint(uint a, uint start, uint end)
{
    return start + (a - start) % (end - start);
}

[numthreads(TN_R,TN_R,1)]
void Render2D(uint3 id : SV_DispatchThreadID)
{
    float pixelPosX = (float)id.x * (float)Width / (float)ResolutionX;
    float pixelPosY = (float)id.y * (float)Height / (float)ResolutionY;
    float2 pixelPos = float2(pixelPosX, pixelPosY);
    int ChunkX = (int)((float)pixelPos.x / (float)MaxInfluenceRadius);
    int ChunkY = (int)((float)pixelPos.y / (float)MaxInfluenceRadius);

    // Render rigid bodies
    int rayHits = 0;
    float rayY = pixelPos.y;
    float2 rayCoordA = float2(0, rayY);
    float2 rayCoordB = float2(Width, rayY);
    for (int RBIndex = 0; RBIndex < RBodiesNum; RBIndex++)
    {
        RBData RBData_i = RBDatas[RBIndex];
        float dstSqr = dot2(pixelPos - RBData_i.pos);
        if (dstSqr > RBData_i.maxRadiusSqr) continue;

        float2 localRayCoordA = rayCoordA - RBData_i.pos;
        float2 localRayCoordB = rayCoordB - RBData_i.pos;
        float localRayPosX = pixelPos.x - RBData_i.pos.x;
        uint startIndex = RBData_i.startIndex;
        uint endIndex = RBData_i.endIndex;
        for (uint lineIndex = startIndex; lineIndex < endIndex; lineIndex++)
        {
            float2 localVecA = RBVectors[lineIndex].pos;
            float2 localVecB = RBVectors[WrapUint(lineIndex+1, startIndex, endIndex)].pos;
            if (CheckLinesIntersect(localVecA, localVecB, localRayCoordA, localRayCoordB))
            { 
                if (LineIntersectionPoint(localVecA, localVecB, localRayCoordA, localRayCoordB).x < localRayPosX)
                {
                    rayHits++; 
                }
            }
        }
        if ((uint)rayHits % 2 == 1) { Result[id.xy] = float4(1, 1, 1, 0.0); return; }
    }

    // Render particle liquids
    float minDst = VisualParticleRadii;
    bool particleFound = false;
    for (int x = -1; x <= 1; x++)
    {
        for (int y = -1; y <= 1; y++)
        {
            int CurChunkX = ChunkX + x;
            int CurChunkY = ChunkY + y;

            if (!ValidChunk(CurChunkX, CurChunkY)) continue;

            int ChunkKey = CurChunkY * ChunksNum.x + CurChunkX;
            int startIndex = StartIndices[ChunkKey];

            int Index = startIndex; 
            while (Index < ParticlesNum && ChunkKey == SpatialLookup[Index].y)
            {
                int pIndex = SpatialLookup[Index].x;
                PData PData_i = PDatas[pIndex];

                float dst = length(pixelPos - PData_i.pos);

                if (dst < minDst)
                {
                    // visualize velocity / temperature
                    float3 rgb = VelocityToRGB(PData_i.Velocity, PTypes[Extract_PType(PData_i.LastChunkKey_PType_POrder)].colorG);
                    // float3 rgb = TemperatureToRGB(PData_i.Temperature);

                    Result[id.xy] = float4(rgb.x, rgb.y, rgb.z, 0.0);
                    minDst = dst;
                    particleFound = true;
                }

                // Increment Index each iteration - Chunk particle search algorithm
                Index ++;
            }
        }
    }
    if (particleFound) return;

    // Background color is r,g,b,o = 0,0,0,0 (black)
    Result[id.xy] = float4(0.0, 0.0, 0.0, 0.0);
}