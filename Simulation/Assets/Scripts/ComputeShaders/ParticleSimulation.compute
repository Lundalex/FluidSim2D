#pragma kernel PreCalculations
#pragma kernel CalculateDensities
#pragma kernel PrepSpringData
#pragma kernel TransferAllSpringData
#pragma kernel ParticleForces
#pragma kernel UpdatePositions
#pragma kernel ConsumeStickynessRequests

#include "MathResources.hlsl"
#include "CompositionResources.hlsl"

static const int ThreadsNum = 512;

// Constants
const int ChunkNumW;
const int ChunkNumH;
const int ChunkNum;
const int PTypesNum;
const int IOOR;
const int Width;
const int Height;
const int ParticlesNum;
const int ParticleSpringsCombinedHalfLength;
const int MaxInfluenceRadius;
const int MaxInfluenceRadiusSqr;
const float InvMaxInfluenceRadius;
const int SpawnDims;
const int TimeStepsPerRender;

const float LookAheadFactor;
const float RBodyElasticity;
const float BorderPadding;
const float MaxInteractionRadius;
const float InteractionAttractionPower;
const float InteractionFountainPower;
const float Plasticity;

// Per-timestep-set constants
const float DeltaTime;
const float SRDeltaTime;
const bool TransferSpringData;

const float MouseX;
const float MouseY;
const bool RMousePressed;
const bool LMousePressed;

// Particles - Buffers
RWStructuredBuffer<int2> SpatialLookup; // [ParticlesNum](particleIndex, ChunkKey)
RWStructuredBuffer<int> StartIndices;

struct SpringStruct
{
    int PLinkedA;
    int PLinkedB;
    float RestLength;
    // float yieldLen;
    // float plasticity;
    // float stiffness;
};
RWStructuredBuffer<SpringStruct> ParticleSpringsCombined; // Result A

const bool FrameBufferCycle;
RWStructuredBuffer<int> ChunkSizes;
RWStructuredBuffer<int2> SpringCapacities; // [](baseChunkCapacity, neighboorChunksCapacity)
RWStructuredBuffer<int> SpringStartIndices_dbA; // Result A
RWStructuredBuffer<int> SpringStartIndices_dbB; // Result B

struct PTypeStruct
{
    float TargetDensity;
    int MaxInfluenceRadius;
    float Pressure;
    float NearPressure;
    float Damping;
    float Viscocity;
    float Elasticity;
    float Plasticity;
    float Stickyness;
    float Gravity;
    float colorG;
};
RWStructuredBuffer<PTypeStruct> PTypes;

struct PDataStruct
{
    float2 PredPosition;
    float2 Position;
    float2 Velocity;
    float2 LastVelocity;
    float Density;
    float NearDensity;
    int LastChunkKey_PType_POrder; // composed 3 int structure
    // POrder; // POrder is dynamic, 
    // LastChunkKey; // 0 <= LastChunkKey <= ChunkNum
    // PType; // 0 <= PType <= PTypeNum
};
RWStructuredBuffer<PDataStruct> PData;

struct StickynessRequestStruct
{
    int pIndex;
    int StickyLineIndex;
    float2 StickyLineDst;
    float absDstToLineSqr;
    float RBStickyness;
    float RBStickynessRange;
};
RWStructuredBuffer<StickynessRequestStruct> SortedStickyRequests;

bool ValidChunk(int ChunkX, int ChunkY)
{
    return ChunkX >= 0 && ChunkX < ChunkNumW && ChunkY >= 0 && ChunkY < ChunkNumH;
}

int GetChunkKey(int chunkX, int chunkY)
{
    return chunkY * ChunkNumW + chunkX;
}

float2 CalcDensity(int pIndex) // (density, nearDensity)
{
    PDataStruct PData_i = PData[pIndex];

    if (TransferSpringData)
    {
        int newChunkX = (int)(PData_i.Position.x * InvMaxInfluenceRadius);
        int newChunkY = (int)(PData_i.Position.y * InvMaxInfluenceRadius);
        int newChunkKey = GetChunkKey(newChunkX, newChunkY);
        Set_LastChunkKey(PData_i.LastChunkKey_PType_POrder, newChunkKey);
        PData[pIndex].LastChunkKey_PType_POrder = PData_i.LastChunkKey_PType_POrder;
    }

    int lastChunkKey = Extract_LastChunkKey(PData_i.LastChunkKey_PType_POrder);

    int chunkX = (uint)lastChunkKey % ChunkNumW;
    int chunkY = (int)((uint)lastChunkKey / ChunkNumW);

    float totDensity = 0.0;
    float totNearDensity = 0.0;
    for (int x = -1; x <= 1; x++)
    {
        for (int y = -1; y <= 1; y++)
        {
            int curChunkX = chunkX + x;
            int curChunkY = chunkY + y;

            if (!ValidChunk(curChunkX, curChunkY)) {continue;}

            int chunkKey = GetChunkKey(curChunkX, curChunkY);
            int startIndex = StartIndices[chunkKey];

            int Index = startIndex;
            while (Index < ParticlesNum && chunkKey == SpatialLookup[Index].y)
            {
                int otherPIndex = SpatialLookup[Index].x;


                // -- Spring handling --

                if (TransferSpringData)
                {
                    if (x == 0 && y == 0)
                    {
                        if (pIndex == otherPIndex)
                        {
                            int pOrder = Index - startIndex;
                            Set_POrder(PData_i.LastChunkKey_PType_POrder, pOrder);
                            PData[pIndex].LastChunkKey_PType_POrder = PData_i.LastChunkKey_PType_POrder;
                        }
                    }
                }


                // -- Calculate densities --

                float dst = length(PData_i.PredPosition - PData[otherPIndex].PredPosition);
                totDensity += SmoothLiquid_optimised(dst);
                totNearDensity += SmoothLiquidNear_optimised(dst);

                // -- Increment Index each iteration - Chunk particle search algorithm --

                Index += 1;
            }
        }
    }
    
    return float2(totDensity, totNearDensity);
}

float4 BoundraryCheck(float2 pos, float2 vel, float extraPadding, float Damping)
{
    float totPadding = BorderPadding + extraPadding;
    float velFactor = 1 - Damping;

    // x
    if (pos.x > Width - totPadding)
    {
        vel.x = -abs(vel.x) * velFactor;
        pos.x = Width - totPadding;
    } else if (pos.x < totPadding) {
        vel.x = abs(vel.x) * velFactor;
        pos.x = totPadding;
    }

    // y
    if (pos.y > Height - totPadding)
    {
        vel.y = -abs(vel.y) * velFactor;
        pos.y = Height - totPadding;
    } else if (pos.y < totPadding) {
        vel.y = abs(vel.y) * velFactor;
        pos.y = totPadding;
    }
    
    return float4(pos.x, pos.y, vel.x, vel.y);
}

float DensityToPressure(float density, float TargetDensity, float Pressure)
{
    float densityError = density - TargetDensity;
    float pressure = densityError * Pressure;
    return pressure;
}

float SharedPressure(float densityA, float densityB, float TargetDensityA, float TargetDensityB, float PressureA, float PressureB)
{
    float pressureA = DensityToPressure(densityA, TargetDensityA, PressureA);
    float pressureB = DensityToPressure(densityB, TargetDensityB, PressureB);
    float sharedPressure = avg(pressureA, pressureB);

    return sharedPressure;
}

float SharedNearPressure(float nearDensityA, float nearDensityB, float NearPressureMultiplierA, float NearPressureMultiplierB)
{
    float nearPressureA = nearDensityA * NearPressureMultiplierA;
    float nearPressureB = nearDensityB * NearPressureMultiplierB;
    float sharedNearPressure = avg(nearPressureA, nearPressureB);

    return sharedNearPressure;
}

float2 PressureForce(float densityA, float densityB, float nearDensityA, float nearDensityB, PTypeStruct PType_i, PTypeStruct PType_otherPIndex, float absDst, float2 normDst, int randSeed)
{
    float absPressureGradient = SmoothLiquidDer_optimised(absDst);
    float absNearPressureGradient = SmoothLiquidNearDer_optimised(absDst);

    float2 pressureGradient = float2(0.0, 0.0);
    float2 nearPressureGradient = float2(0.0, 0.0);

    if (absDst == 0) {
        // semirandom direction, not normalized
        float2 randVector = float2(randSeed % 0.28, randSeed % 0.18);

        pressureGradient = 0.05 * randVector * absPressureGradient;
        nearPressureGradient = 0.05 * randVector * absNearPressureGradient;
    }
    else {
        pressureGradient = normDst * absPressureGradient;
        nearPressureGradient = normDst * absNearPressureGradient;
    }

    float avgPressure = SharedPressure(densityA, densityB, PType_i.TargetDensity, PType_otherPIndex.TargetDensity, PType_i.Pressure, PType_otherPIndex.Pressure);

    float avgNearPressure = SharedNearPressure(nearDensityA, nearDensityB, PType_i.NearPressure, PType_otherPIndex.NearPressure);

    if (densityA + densityB == 0) {return float2(0.0, 0.0);}
    float2 pressureForce = (avgPressure * pressureGradient + avgNearPressure * nearPressureGradient) / avg(densityA, densityB);

    return pressureForce;
}

float2 ViscocityForce(float2 LastVelocityA, float2 LastVelocityB, PTypeStruct PType_i, PTypeStruct PType_otherPIndex, float absDst)
{
    float2 viscocityForce = (LastVelocityB - LastVelocityA) * SmoothViscosityLaplacian_optimised(absDst);

    return viscocityForce * avg(PType_i.Viscocity, PType_otherPIndex.Viscocity);
}

float2 InteractionForce(int pIndex)
{
    if (!LMousePressed && !RMousePressed) {return float2(0.0, 0.0);}
    int dir = 0;
    if (LMousePressed) {dir = -1;}
    else if (RMousePressed) {dir = 1;}

    float2 dst = PData[pIndex].PredPosition - float2(MouseX, MouseY);

    float absDst = length(dst);

    if (absDst > MaxInteractionRadius) {return float2(0.0, 0.0);}

    float absInteractionGradient = SmoothLiquid_optimised(absDst);

    if (absDst == 0) {return float2(0.0, 0.0);}

    float2 interactionGradient = normalize(dst) * absInteractionGradient;

    float2 baseInteractionForce = interactionGradient * InteractionAttractionPower * dir;
    float fountainInteractionForce = 0;
    if (dir == -1)
    {
        float dstX = MaxInteractionRadius - abs(dst.x);
        fountainInteractionForce = InteractionFountainPower * absInteractionGradient * dstX;
    }

    return baseInteractionForce + float2(0.0, fountainInteractionForce);
}

float2 StickynessForce(PDataStruct PData_i, PTypeStruct PType_i, StickynessRequestStruct stickynessRequest)
{
    float absDstToLine = sqrt(stickynessRequest.absDstToLineSqr);

    float Stickyness = stickynessRequest.RBStickyness * PType_i.Stickyness;
    float absStickynessForce = RBPStickynessModel(Stickyness, absDstToLine, stickynessRequest.RBStickynessRange);

    // If the lenSqr of a vector is 0, normalized(0) does a /0 operation, leading to thread errors
    // Over time, this decreases the amount of available threads, leading to decreased performance!
    if (dot(stickynessRequest.StickyLineDst, stickynessRequest.StickyLineDst) == 0) { return float2(0, 0); }

    float2 normDstToLine = normalize(stickynessRequest.StickyLineDst);
    float2 stickynessForce = absStickynessForce * normDstToLine;
    
    return stickynessForce;
}

float2 LiquidSpringForce(PTypeStruct PType_i, PTypeStruct PType_otherPIndex, int pIndex, int otherPIndex, float absDst, float2 normDst, int springIndex)
{
    SpringStruct spring = ParticleSpringsCombined[springIndex];

    // if spring is not already initialized, initialize a new one
    if (spring.PLinkedA == -1) { spring.PLinkedA = pIndex; spring.PLinkedB = otherPIndex; spring.RestLength = MaxInfluenceRadius; }


    // -- Modify spring and perform force calculations (plasticity, elasticity) --

    // Plasticity calculations
    float diffMng = absDst - spring.RestLength;
    float absDiffMng = abs(diffMng);
    int sgnDiffMng = sign(diffMng);
    float plasticityConst = 8;
    float tolDeformation = 0.0; // = yieldRatio / restLength

    // ∆L = ∆t * α * sign(r − L) * max(0,|r −L| − γL)
    spring.RestLength += LiquidSpringPlasticityModel(plasticityConst, sgnDiffMng, absDiffMng, tolDeformation, DeltaTime);

    // Elasticity calculations
    float springStiffness = Plasticity;
    float absSpringForce = LiquidSpringForceModel(springStiffness, spring.RestLength, MaxInfluenceRadius, absDst);
    float2 springForce = absSpringForce * normDst;


    // -- Write new spring data --

    ParticleSpringsCombined[springIndex] = spring;


    // -- Return calculated force --

    return (isnan(springForce.x) || isnan(springForce.y)) ? float2(0.0, 0.0) : -springForce;
}

[numthreads(ThreadsNum,1,1)]
void PreCalculations (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;

    PData[i].PredPosition = PData[i].Position + PData[i].Velocity * LookAheadFactor;

    // Make sure PredPositions[i] is within the world boundrary
    PData[i].PredPosition.x = clamp(PData[i].PredPosition.x, BorderPadding, Width - BorderPadding);
    PData[i].PredPosition.y = clamp(PData[i].PredPosition.y, BorderPadding, Height - BorderPadding);

    // LastVelocities
    PData[i].LastVelocity = PData[i].Velocity;
}

[numthreads(ThreadsNum,1,1)]
void CalculateDensities (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticlesNum) { return; }
    // Index SpatialLookup to improve memory access coherency
    int i = SpatialLookup[id.x].x;

    // density
    float2 Dns = CalcDensity(i);
    PData[i].Density = Dns.x;
    PData[i].NearDensity = Dns.y;
}

[numthreads(ThreadsNum,1,1)]
void PrepSpringData (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticleSpringsCombinedHalfLength) { return; }

    int i = FrameBufferCycle
    ? id.x + ParticleSpringsCombinedHalfLength
    : id.x;

    ParticleSpringsCombined[i].PLinkedA = -1;
}

[numthreads(ThreadsNum,1,1)]
void TransferAllSpringData (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticleSpringsCombinedHalfLength) { return; }
    int i = FrameBufferCycle
    ? id.x
    : id.x + ParticleSpringsCombinedHalfLength;

    // last -> new
    // this is not a solution for adding springs - default spring data should be set before this shader pass

    // B is connected to A
    SpringStruct lastSpring = ParticleSpringsCombined[i];
    if (lastSpring.PLinkedA == -1) { return; }
    PDataStruct PData_A = PData[lastSpring.PLinkedA];
    PDataStruct PData_B = PData[lastSpring.PLinkedB];

    float2 PosDiff = PData_A.Position - PData_B.Position;
    float absPosDiffSqr = dot(PosDiff, PosDiff);

    // If A and B are in range of each other, transfer data. Otherwise, do not (spring data is deleted)
    if (absPosDiffSqr <= MaxInfluenceRadiusSqr)
    {
        int pOrder_A = Extract_POrder(PData_A.LastChunkKey_PType_POrder);
        int pOrder_B = Extract_POrder(PData_B.LastChunkKey_PType_POrder);
        int lastChunkKey_A = Extract_LastChunkKey(PData_A.LastChunkKey_PType_POrder);
        int lastChunkKey_B = Extract_LastChunkKey(PData_B.LastChunkKey_PType_POrder);
    
        int newChunkX_A = (uint)lastChunkKey_A % ChunkNumW;
        int newChunkY_A = (int)((float)lastChunkKey_A / ChunkNumW);

        int newChunkX_B = (uint)lastChunkKey_B % ChunkNumW;
        int newChunkY_B = (int)((float)lastChunkKey_B / ChunkNumW);

        // not optimal since the same calculations are performed by multiple threads (many springs in each chunk)
        int localSpringBCapacityOrder = 0;
        bool inRangeAB = false;
        bool shouldBreak = false;
        for (int x = -1; x <= 1 && !shouldBreak; x++)
        {
            int curChunkX = newChunkX_A + x;

            for (int y = -1; y <= 1 && !shouldBreak; y++)
            {
                int curChunkY = newChunkY_A + y;

                if (ValidChunk(curChunkX, curChunkY))
                {
                    if (curChunkX == newChunkX_B && curChunkY == newChunkY_B) { inRangeAB = true; shouldBreak = true; break; }
                    int curChunkKey = GetChunkKey(curChunkX, curChunkY);
                    localSpringBCapacityOrder += SpringCapacities[curChunkKey].x;
                }
            }
        }
        if (inRangeAB)
        {
            int newNeighboorNum = localSpringBCapacityOrder + pOrder_B;

            int newChunkKey_A = GetChunkKey(newChunkX_A, newChunkY_A);

            int nearSpringCapacity = (uint)SpringCapacities[newChunkKey_A].y / SpringCapacities[newChunkKey_A].x;
            int newSpringIndex = FrameBufferCycle
            ? SpringStartIndices_dbA[max(newChunkKey_A-1, 0)] + pOrder_A * nearSpringCapacity + newNeighboorNum + ParticleSpringsCombinedHalfLength
            : SpringStartIndices_dbB[max(newChunkKey_A-1, 0)] + pOrder_A * nearSpringCapacity + newNeighboorNum;

            ParticleSpringsCombined[newSpringIndex] = lastSpring;
        }
    }
}

[numthreads(ThreadsNum,1,1)]
void ParticleForces (uint3 id : SV_DispatchThreadID)
{
    if (id.x >= (uint)ParticlesNum) { return; }
    // Index SpatialLookup to improve memory access coherency
    int i = SpatialLookup[id.x].x;

    // Can probably be optimised by using if (i > otherPIndex) to avoid calculating each pair twice, or by using data links (springs)

    PDataStruct PData_i = PData[i];
    PTypeStruct PType_i = PTypes[Extract_PType(PData_i.LastChunkKey_PType_POrder)];
    int pOrder = Extract_POrder(PData_i.LastChunkKey_PType_POrder);
    int lastChunkKey = Extract_LastChunkKey(PData_i.LastChunkKey_PType_POrder);

    int chunkX = (uint)lastChunkKey % ChunkNumW;
    int chunkY = (int)((uint)lastChunkKey / ChunkNumW);
    int baseChunkKey = GetChunkKey(chunkX, chunkY);

    int nearSpringCapacity = (uint)SpringCapacities[baseChunkKey].y / SpringCapacities[baseChunkKey].x;

    float2 totPressureForce = float2(0.0, 0.0);
    float2 totViscocityForce = float2(0.0, 0.0);
    float2 totSpringForce = float2(0.0, 0.0);
    int nNum = 0;
    for (int x = -1; x <= 1; x++)
    {
        int curChunkX = chunkX + x;

        for (int y = -1; y <= 1; y++)
        {
            int curChunkY = chunkY + y;

            if (!ValidChunk(curChunkX, curChunkY)) { continue; }

            int chunkKey = GetChunkKey(curChunkX, curChunkY);
            int startIndex = StartIndices[chunkKey];

            int Index = startIndex; 
            while (Index < ParticlesNum && chunkKey == SpatialLookup[Index].y)
            {
                int otherPIndex = SpatialLookup[Index].x;
                if (i == otherPIndex) { Index += 1; nNum += 1; continue; }
                PDataStruct PData_otherPIndex = PData[otherPIndex];
                PTypeStruct PType_otherPIndex = PTypes[Extract_PType(PData_otherPIndex.LastChunkKey_PType_POrder)];

                float2 dst = PData_otherPIndex.Position - PData_i.Position;
                float sqrDst = dot(dst, dst);

                if (sqrDst > MaxInfluenceRadiusSqr) { Index += 1; nNum += 1; continue; }
                float absDst = sqrt(sqrDst);
                float2 normDst = normalize(dst);

                int springIndex = FrameBufferCycle
                ? SpringStartIndices_dbA[max(baseChunkKey-1, 0)] + pOrder * nearSpringCapacity + nNum + ParticleSpringsCombinedHalfLength
                : SpringStartIndices_dbB[max(baseChunkKey-1, 0)] + pOrder * nearSpringCapacity + nNum;

                totPressureForce += PressureForce(PData_i.Density, PData_otherPIndex.Density, PData_i.NearDensity, PData_otherPIndex.NearDensity, PType_i, PType_otherPIndex, absDst, normDst, otherPIndex);
                totViscocityForce += ViscocityForce(PData_i.LastVelocity, PData_otherPIndex.LastVelocity, PType_i, PType_otherPIndex, absDst);
                totSpringForce += LiquidSpringForce(PType_i, PType_otherPIndex, i, otherPIndex, absDst, normDst, springIndex);


                // -- Increment Index each iteration - particle chunks algorithm, particle springs algorithm --

                Index += 1;
                nNum += 1;
            }
        }
    }

    float2 interactionForce = InteractionForce(i);

    float2 totForce = totPressureForce + totViscocityForce + totSpringForce + interactionForce + float2(0, -PType_i.Gravity);
    PData[i].Velocity += totForce * DeltaTime;
}

[numthreads(ThreadsNum,1,1)]
void UpdatePositions (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;

    PDataStruct PData_i = PData[i];

    PData_i.Position += PData_i.Velocity * DeltaTime;

    float4 PosVelData = BoundraryCheck(PData_i.Position, PData_i.Velocity, 0, PTypes[Extract_PType(PData_i.LastChunkKey_PType_POrder)].Damping);
    PData[i].Position = float2(PosVelData.x, PosVelData.y);
    PData[i].Velocity = float2(PosVelData.z, PosVelData.w);
}

[numthreads(512,1,1)]
void ConsumeStickynessRequests (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;

    StickynessRequestStruct stickynessRequest = SortedStickyRequests[i];
    int pIndex = stickynessRequest.pIndex;

    // This does many "unnecessary" iterations for every thread, but thread management complexity gets reduced
    float smallestDstSqr = 100;
    for (int offset = -3; offset <= 3; offset++) {
        if (offset == 0) continue;

        int idx = i + offset;
        if (idx > 0 && idx < 4098) {
            if (SortedStickyRequests[idx].pIndex == pIndex) {
                if (SortedStickyRequests[idx].absDstToLineSqr < smallestDstSqr) {
                    smallestDstSqr = SortedStickyRequests[idx].absDstToLineSqr;
                }
            }
        }
    }

    if (stickynessRequest.absDstToLineSqr <= smallestDstSqr) {
        PDataStruct PData_i = PData[pIndex];
        PTypeStruct PType_i = PTypes[Extract_PType(PData_i.LastChunkKey_PType_POrder)];
        float2 stickynessForce = StickynessForce(PData_i, PType_i, stickynessRequest);
    
        PData[pIndex].Velocity += stickynessForce * DeltaTime;  
    }
}