#pragma kernel PreCalculations
#pragma kernel CalculateDensities
#pragma kernel ParticleForces
#pragma kernel UpdatePositions
#pragma kernel ConsumeStickynessRequests

#include "MathResources.hlsl"

static const int ThreadsNum = 512;

// Constants
const int ChunkNumW;
const int ChunkNumH;
const int IOOR;
const int Width;
const int Height;
const int ParticlesNum;
const int RBodiesNum;
const int MaxInfluenceRadius;
const int MaxInfluenceRadiusSqr;
const float InvMaxInfluenceRadius;
const int SpawnDims;
const int TimeStepsPerRender;

const float LookAheadFactor;
const float RBodyElasticity;
const float BorderPadding;
const float MaxInteractionRadius;
const float InteractionAttractionPower;
const float InteractionFountainPower;
const int SpringCapacity;
const float Plasticity;

// Per-timestep-set constants
const float DeltaTime;
const float SRDeltaTime;

const float MouseX;
const float MouseY;
const bool RMousePressed;
const bool LMousePressed;

// Particles - Buffers
RWStructuredBuffer<int2> SpatialLookup; // [ParticlesNum](particleIndex, ChunkKey)
RWStructuredBuffer<int> StartIndices;

struct SpringStruct
{
    int linkedIndex;
    float restLength;
    // float yieldLen;
    // float plasticity;
    // float stiffness;
};
RWStructuredBuffer<SpringStruct> SpringPairs;

struct PTypeStruct
{
    float TargetDensity;
    int MaxInfluenceRadius;
    float Pressure;
    float NearPressure;
    float Damping;
    float Viscocity;
    float Elasticity;
    float Plasticity;
    float Stickyness;
    float Gravity;
    float colorG;
};
RWStructuredBuffer<PTypeStruct> PTypes;

struct PDataStruct
{
    float2 PredPosition;
    float2 Position;
    float2 Velocity;
    float2 LastVelocity;
    float Density;
    float NearDensity;
    int PType;
};
RWStructuredBuffer<PDataStruct> PData;

struct RBDataStruct
{
    float2 Position;
    float2 Velocity;
    // radians / second
    float AngularImpulse;
    float Stickyness;
    float StickynessRange;
    float StickynessRangeSqr;
    float2 NextPos;
    float2 NextVel;
    float NextAngImpulse;
    float Mass;
    int2 LineIndices;
    float MaxDstSqr;
    int WallCollision;
    int Stationary; // 1 -> Stationary, 0 -> Non-stationary
};
RWStructuredBuffer<RBDataStruct> RBData;

struct RBVectorStruct
{
    float2 Position;
    float2 LocalPosition;
    float3 ParentImpulse;
    int ParentRBIndex;
    int WallCollision;
};
RWStructuredBuffer<RBVectorStruct> RBVector;

struct StickynessRequestStruct
{
    int pIndex;
    int StickyLineIndex;
    float2 StickyLineDst;
    float absDstToLineSqr;
    float RBStickyness;
    float RBStickynessRange;
};
RWStructuredBuffer<StickynessRequestStruct> SortedStickyRequests;

bool ValidChunk(int ChunkX, int ChunkY)
{
    if (ChunkX >= 0 && ChunkX < ChunkNumW && ChunkY >= 0 && ChunkY < ChunkNumH) {return true;}
    return false;
}

bool IsInfluencedChunk(int x, int y, int localPosX, int localPosY)
{
    if (x != 0 && y != 0)
    {
        int xUse = 0;
        int yUse = 0;
        if (x == 1 && y == 1) { xUse = 1; yUse = 1; }
        else if (x == -1 && y == -1) { xUse = 0; yUse = 0; }
        else if (x == 1 && y == -1) { xUse = 1; yUse = 0; }
        else if (x == -1 && y == 1) { xUse = 0; yUse = 1; }
        float2 dst = float2(localPosX - xUse, localPosY - yUse);
        float absDstSqr = dot(dst, dst);
        if (absDstSqr > MaxInfluenceRadiusSqr) { return false; }
    }
    return true;
}

float2 CalcDensity(int pIndex) // (density, nearDensity)
{
    PDataStruct PData_i = PData[pIndex];
    // Int type conversion removes decimals, effectively doing a Floor() operation
    int ChunkX = (int)(PData_i.PredPosition.x / MaxInfluenceRadius);
    int ChunkY = (int)(PData_i.PredPosition.y / MaxInfluenceRadius);

    // float localPosX = (PData_i.PredPosition.x % MaxInfluenceRadius) * InvMaxInfluenceRadius;
    // float localPosY = (PData_i.PredPosition.y % MaxInfluenceRadius) * InvMaxInfluenceRadius;
    float totDensity = 0.0;
    float totNearDensity = 0.0;
    for (int x = -1; x <= 1; x++)
    {
        for (int y = -1; y <= 1; y++)
        {
            int CurChunkX = ChunkX + x;
            int CurChunkY = ChunkY + y;

            // if (x != 0 && y != 0)
            // {
            //     int xUse = 0;
            //     int yUse = 0;
            //     if (x == 1 && y == 1) { xUse = 1; yUse = 1; }
            //     else if (x == -1 && y == -1) { xUse = 0; yUse = 0; }
            //     else if (x == 1 && y == -1) { xUse = 1; yUse = 0; }
            //     else if (x == -1 && y == 1) { xUse = 0; yUse = 1; }
            //     float2 dst = float2(localPosX - xUse, localPosY - yUse);
            //     float absDstSqr = dot(dst, dst);
            //     if (absDstSqr > MaxInfluenceRadiusSqr) { continue; }
            // }
            
            if (!ValidChunk(CurChunkX, CurChunkY)) {continue;}

            int ChunkKey = CurChunkY * ChunkNumW + CurChunkX;
            int startIndex = StartIndices[ChunkKey];

            int Index = startIndex;
            while (Index < ParticlesNum && ChunkKey == SpatialLookup[Index].y)
            {
                int otherPIndex = SpatialLookup[Index].x;

                float dst = length(PData_i.PredPosition - PData[otherPIndex].PredPosition);

                totDensity += SmoothLiquid(dst, MaxInfluenceRadius);
                totNearDensity += SmoothLiquidNear(dst, MaxInfluenceRadius);

                // Increment Index each iteration - Chunk particle search algorithm
                Index += 1;
            }
        }
    }
    
    return float2(totDensity, totNearDensity);
}

float4 BoundraryCheck(float2 pos, float2 vel, float extraPadding, float Damping)
{
    float totPadding = BorderPadding + extraPadding;
    float velFactor = 1 - Damping;

    if (pos.y > Height - totPadding)
    {
        vel.y = -abs(vel.y) * velFactor;
        pos.y = Height - totPadding;
    }
    else if (pos.y < totPadding)
    {
        vel.y = abs(vel.y) * velFactor;
        pos.y = totPadding;
    }
    if (pos.x > Width - totPadding)
    {
        vel.x = -abs(vel.x) * velFactor;
        pos.x = Width - totPadding;
    }
    else if (pos.x < totPadding)
    {
        vel.x = abs(vel.x) * velFactor;
        pos.x = totPadding;
    }
    
    return float4(pos.x, pos.y, vel.x, vel.y);
}

float DensityToPressure(float density, float TargetDensity, float Pressure)
{
    float densityError = density - TargetDensity;
    float pressure = densityError * Pressure;
    return pressure;
}

float SharedPressure(float densityA, float densityB, float TargetDensityA, float TargetDensityB, float PressureA, float PressureB)
{
    float pressureA = DensityToPressure(densityA, TargetDensityA, PressureA);
    float pressureB = DensityToPressure(densityB, TargetDensityB, PressureB);
    float sharedPressure = avg(pressureA, pressureB);

    return sharedPressure;
}

float SharedNearPressure(float nearDensityA, float nearDensityB, float NearPressureMultiplierA, float NearPressureMultiplierB)
{
    float nearPressureA = nearDensityA * NearPressureMultiplierA;
    float nearPressureB = nearDensityB * NearPressureMultiplierB;
    float sharedNearPressure = avg(nearPressureA, nearPressureB);

    return sharedNearPressure;
}

float2 PressureForce(float densityA, float densityB, float nearDensityA, float nearDensityB, PTypeStruct PType_i, PTypeStruct PType_otherPIndex, float absDst, float2 dst, int randSeed)
{
    float absPressureGradient = SmoothLiquidDer(absDst, MaxInfluenceRadius);
    float absNearPressureGradient = SmoothLiquidNearDer(absDst, MaxInfluenceRadius);

    float2 pressureGradient = float2(0.0, 0.0);
    float2 nearPressureGradient = float2(0.0, 0.0);

    if (absDst == 0) {
        // semirandom direction, not normalized
        float2 randVector = float2(randSeed % 0.2842, randSeed % 0.1827);

        pressureGradient = 0.05 * randVector * absPressureGradient;
        nearPressureGradient = 0.05 * randVector * absNearPressureGradient;
    }
    else {
        pressureGradient = normalize(dst) * absPressureGradient;
        nearPressureGradient = normalize(dst) * absNearPressureGradient;
    }

    float avgPressure = SharedPressure(densityA, densityB, PType_i.TargetDensity, PType_otherPIndex.TargetDensity, PType_i.Pressure, PType_otherPIndex.Pressure);

    float avgNearPressure = SharedNearPressure(nearDensityA, nearDensityB, PType_i.NearPressure, PType_otherPIndex.NearPressure);

    if (densityA + densityB == 0) {return float2(0.0, 0.0);}
    float2 pressureForce = (avgPressure * pressureGradient + avgNearPressure * nearPressureGradient) / avg(densityA, densityB);

    return pressureForce;
}

float2 ViscocityForce(float2 LastVelocityA, float2 LastVelocityB, PTypeStruct PType_i, PTypeStruct PType_otherPIndex, float absDst)
{
    float2 viscocityForce = (LastVelocityB - LastVelocityA) * SmoothViscosityLaplacian(absDst, MaxInfluenceRadius);

    return viscocityForce * avg(PType_i.Viscocity, PType_otherPIndex.Viscocity);
}

float2 StickynessForce(PDataStruct PData_i, PTypeStruct PType_i, StickynessRequestStruct stickynessRequest)
{
    float absDstToLine = sqrt(stickynessRequest.absDstToLineSqr);

    float Stickyness = stickynessRequest.RBStickyness * PType_i.Stickyness;
    float absStickynessForce = RBPStickynessModel(Stickyness, absDstToLine, stickynessRequest.RBStickynessRange);

    float2 normDstToLine = normalize(stickynessRequest.StickyLineDst);
    float2 stickynessForce = absStickynessForce * normDstToLine;
    
    return stickynessForce;
}

void AddSpring(int springKey, int otherPIndex, float springRestLen)
{
    SpringPairs[springKey].linkedIndex = otherPIndex;
    SpringPairs[springKey].restLength = springRestLen;
}

void DeleteSpring(int springKey, int otherPIndex, int maxIterationCount)
{
    int endIndex = springKey + maxIterationCount;
    for (int springIndex = springKey; springIndex < endIndex; springIndex++)
    {
        if (SpringPairs[springIndex].linkedIndex == otherPIndex)
        {
            SpringPairs[springIndex].linkedIndex = IOOR;
            return;
        }
    }
}

float2 LiquidSpringForce(PTypeStruct PType_i, PTypeStruct PType_otherPIndex, int pIndex, int otherPIndex, float absDst, float2 dst)
{
    // if (absDst > MaxInfluenceRadius-0.5) {return float2(0,0);}

    // // eliminate duplicate springs between particles
    // // if (pIndex < otherPIndex) {return;}

    // // int springKey = pIndex * SpringCapacity + (uint)otherPIndex % SpringCapacity;

    // int iteration = 0;
    // while (SpringPairs[springKey].linkedIndex != otherPIndex && SpringPairs[springKey].linkedIndex != IOOR && iteration < MaxIterationCount)
    // {
    //     springKey++;
    //     iteration++;
    // }
    // // if (iteration == MaxIterationCount) {return float2(0.0, 0.0);}
 
    // if (SpringPairs[springKey].linkedIndex != IOOR) {AddSpring(springKey, otherPIndex, 1);}
 
    // float diffMng = absDst - SpringPairs[springKey].restLength;
    // int absDiffMng = abs(diffMng);
    // int sgnDiffMng = sign(diffMng);
    // int plasticityConst = 0.7;
    // float tolDeformation = 0; // = yieldRatio / restLength
    // // ∆L = ∆t * α * sign(r − L) * max(0,|r −L| − γL)
    // SpringPairs[springKey].restLength -= plasticityConst * sgnDiffMng * max(0, absDiffMng - tolDeformation) * DeltaTime;

    float springStiffness = Plasticity;
    float2 normDst = normalize(dst);                             // SpringPairs[springKey].restLength
    float absSpringForce = LiquidSpringForceModel(springStiffness, (uint)MaxInfluenceRadius/2, MaxInfluenceRadius, absDst);
 
    float2 springForce = absSpringForce * normDst * 0.5;
    return -springForce * DeltaTime;
}

float2 InteractionForce(int pIndex)
{
    if (!LMousePressed && !RMousePressed) {return float2(0.0, 0.0);}
    int dir = 0;
    if (LMousePressed) {dir = -1;}
    else if (RMousePressed) {dir = 1;}

    float2 dst = PData[pIndex].PredPosition - float2(MouseX, MouseY);

    float absDst = length(dst);

    if (absDst > MaxInteractionRadius) {return float2(0.0, 0.0);}

    float absInteractionGradient = SmoothLiquid(absDst, MaxInteractionRadius);

    if (absDst == 0) {return float2(0.0, 0.0);}

    float2 interactionGradient = normalize(dst) * absInteractionGradient;

    float2 baseInteractionForce = interactionGradient * InteractionAttractionPower * dir;
    float fountainInteractionForce = 0;
    if (dir == -1)
    {
        float dstX = MaxInteractionRadius - abs(dst.x);
        fountainInteractionForce = InteractionFountainPower * absInteractionGradient * dstX;
    }

    return baseInteractionForce + float2(0.0, fountainInteractionForce);
}

[numthreads(ThreadsNum,1,1)]
void PreCalculations (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;

    PData[i].PredPosition = PData[i].Position + PData[i].Velocity * LookAheadFactor;
    // Make sure PredPositions[i] is within the world boundrary
    PData[i].PredPosition.x = max(min(PData[i].PredPosition.x, Width - 0.1), 0.1);
    PData[i].PredPosition.y = max(min(PData[i].PredPosition.y, Height - 0.1), 0.1);

    // LastVelocities
    PData[i].LastVelocity = PData[i].Velocity;
}

[numthreads(ThreadsNum,1,1)]
void CalculateDensities (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;
    // density
    float2 Dns = CalcDensity(i);
    PData[i].Density = Dns.x;
    PData[i].NearDensity = Dns.y;
}

[numthreads(ThreadsNum,1,1)]
void ParticleForces (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;

    // Can probably be optimised by using if (i > otherPIndex) to avoid calculating each pair twice

    // Int type conversion removes decimals, effectively doing a Floor() operation
    PDataStruct PData_i = PData[i];
    PTypeStruct PType_i = PTypes[PData_i.PType];
    int ChunkX = (int)(PData_i.PredPosition.x * InvMaxInfluenceRadius);
    int ChunkY = (int)(PData_i.PredPosition.y * InvMaxInfluenceRadius);

    float localPosX = (PData_i.PredPosition.x % MaxInfluenceRadius) * InvMaxInfluenceRadius;
    float localPosY = (PData_i.PredPosition.y % MaxInfluenceRadius) * InvMaxInfluenceRadius;
    float2 totPressureForce = float2(0.0, 0.0);
    float2 totViscocityForce = float2(0.0, 0.0);
    float2 totSpringForce = float2(0.0, 0.0);
    for (int x = -1; x <= 1; x++)
    {
        for (int y = -1; y <= 1; y++)
        {
            int CurChunkX = ChunkX + x;
            int CurChunkY = ChunkY + y;

            // This does the same as IsInfluencedChunk(), but the funtion call is too costly in terms of performance
            if (x != 0 && y != 0)
            {
                int xUse = 0;
                int yUse = 0;
                if (x == 1 && y == 1) { xUse = 1; yUse = 1; }
                else if (x == -1 && y == -1) { xUse = 0; yUse = 0; }
                else if (x == 1 && y == -1) { xUse = 1; yUse = 0; }
                else if (x == -1 && y == 1) { xUse = 0; yUse = 1; }
                float2 dst = float2(localPosX - xUse, localPosY - yUse);
                float absDstSqr = dot(dst, dst);
                if (absDstSqr > MaxInfluenceRadiusSqr) { continue; }
            }
            // if (!IsInfluencedChunk(x, y, localPosX, localPosY)) { continue; }

            if (!ValidChunk(CurChunkX, CurChunkY)) { continue; }

            int ChunkKey = CurChunkY * ChunkNumW + CurChunkX;
            int startIndex = StartIndices[ChunkKey];

            int Index = startIndex; 
            while (Index < ParticlesNum && ChunkKey == SpatialLookup[Index].y)
            {
                int otherPIndex = SpatialLookup[Index].x;
                if (i == otherPIndex) { Index += 1; continue; }
                PDataStruct PData_otherPIndex = PData[otherPIndex];
                PTypeStruct PType_otherPIndex = PTypes[PData_otherPIndex.PType];

                float2 dst = PData_otherPIndex.PredPosition - PData_i.PredPosition;
                float sqrDst = dot(dst, dst);
                // float absDst = sqrt(sqrDst);

                // int MaxIterationCount = 2;
                // int springKey = i * SpringCapacity + (uint)otherPIndex % SpringCapacity;
                // if (absDst > 1)
                // {
                //     DeleteSpring(springKey, otherPIndex, MaxIterationCount); 
                // }

                if (sqrDst > MaxInfluenceRadiusSqr) { Index += 1; continue; }
                float absDst = sqrt(sqrDst);
                
                totPressureForce += PressureForce(PData_i.Density, PData_otherPIndex.Density, PData_i.NearDensity, PData_otherPIndex.NearDensity, PType_i, PType_otherPIndex, absDst, dst, otherPIndex);
                totViscocityForce += ViscocityForce(PData_i.LastVelocity, PData_otherPIndex.LastVelocity, PType_i, PType_otherPIndex, absDst); 
                totSpringForce += LiquidSpringForce(PType_i, PType_otherPIndex, i, otherPIndex, absDst, dst);

                // Increment Index each iteration - Chunk particle search algorithm
                Index += 1;
            }
        }
    }

    float2 interactionForce = InteractionForce(i);

    float2 totForce = totPressureForce + totViscocityForce + totSpringForce + interactionForce + float2(0, -PType_i.Gravity);
    PData[i].Velocity += totForce * DeltaTime;
}

[numthreads(ThreadsNum,1,1)]
void UpdatePositions (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;

    PDataStruct PData_i = PData[i];

    PData_i.Position += PData_i.Velocity * DeltaTime;

    float4 PosVelData = BoundraryCheck(PData_i.Position, PData_i.Velocity, 0, PTypes[PData_i.PType].Damping);
    PData[i].Position = float2(PosVelData.x, PosVelData.y);
    PData[i].Velocity = float2(PosVelData.z, PosVelData.w);
}


[numthreads(ThreadsNum,1,1)]
void ConsumeStickynessRequests (uint3 id : SV_DispatchThreadID)
{
    int i = id.x;
    if (i == 0) { return; }

    StickynessRequestStruct stickynessRequest = SortedStickyRequests[i];

    int pIndex = stickynessRequest.pIndex;

    int offset = 1;
    float smallestDstSqr = 100;
    while (SortedStickyRequests[i+offset].pIndex == pIndex && i+offset+1 < 4098)
    {
        if (SortedStickyRequests[i+offset].absDstToLineSqr < smallestDstSqr)
        {
            smallestDstSqr = SortedStickyRequests[i+offset].absDstToLineSqr;
        }
        offset++;
    }
    offset = 1;
    while (SortedStickyRequests[i-offset].pIndex == pIndex && i-offset-1 > 0)
    {
        if (SortedStickyRequests[i-offset].absDstToLineSqr < smallestDstSqr)
        {
            smallestDstSqr = SortedStickyRequests[i+offset].absDstToLineSqr;
        }
        offset++;
    }

    if (stickynessRequest.absDstToLineSqr > smallestDstSqr) { return; }
    else
    {
        PDataStruct PData_i = PData[pIndex];
        PTypeStruct PType_i = PTypes[PData_i.PType];
        float2 stickynessForce = StickynessForce(PData_i, PType_i, stickynessRequest);
    
        PData[pIndex].Velocity += stickynessForce * DeltaTime;  
    }
}